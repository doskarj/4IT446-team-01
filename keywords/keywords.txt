*** Settings ***
Library    Selenium2Library
Resource    ../variables/variables.txt 
Resource    ../objects/objects_vydra.txt 
Resource    ../objects/objects_search.txt
Resource    ../objects/objects_success_msg.txt
Resource    ../objects/objects_shopping_cart.txt
Resource    ../objects/objects_shop_process.txt
*** Keywords ***
Open Browser To Zoo Eshop
    Open Browser    https://eshop.zoopraha.cz    browser=chrome
    Maximize Browser Window
    ${page_title}=    Get Title
    Should Contain    ${page_title}    ${eshop_page_title}
    
Navigate To Souveniers/magnets
    Wait Until Element Is Visible    ${suveniers_url}
    Click Element    ${suveniers_url}
    Wait Until Element Is Visible    ${magnets_category}
    Click Element    ${magnets_category}
    ${page_title}=    Get Title
    Should Contain    ${page_title}    ${page_title_magnets}
    ${url}=    Get Location
    Should Be Equal    ${url}    ${magnets_page_url}
    
Check For Product In List
    Wait Until Element Is Visible    ${vydra_url}
    Element Text Should Be    ${product_list_item}    ${product_name}
    Element Should Contain    xpath=//div[@class='item-area' and //h2/a[@title='${product_name}']]//span[@id='price-including-tax-1361']    ${product_price}
    Page Should Contain Element    xpath=//div[@class='item-area' and //h2/a[@title='${product_name}']]//img[@alt='${product_img_alt}']
    
Valid Success Message
    Wait Until Element Is Visible    ${success_msg_containter}
    Element Should Contain    ${success_msg_product}    ${product_name}
    Page Should Contain Element    ${success_msg_checkout}
    Page Should Contain Element    ${success_msg_continue}
    
Step From Success Message To Shopping Cart
    Click Element    ${success_msg_checkout}
    Page Should Contain Element    ${shop_cart_table}
    
Check Header Menu
    Page Should Contain Element    ${header_menu}
    
Add Product To Shopping Cart From Product List
     Mouse Over    xpath=//div[@class='item-area' and //h2/a[@title='${product_name}']]
    Click Link    xpath=//div[@class='item-area' and //h2/a[@title='${product_name}']]//a[@title='Přidat do košíku']
    
Check Product Detail Basic Info
    Element Text Should Be    ${vydra_title}    ${productName}
    Page Should Contain Element    ${vydra_img}    
    Page Should Contain Element    ${vydra_availabilily}
    Element Should Contain    ${vydra_price}    ${productPrice}
    Page Should Contain Element    ${vydra_qty}
    Page Should Contain Button    ${vydra_add_to_cart} 
    
    
Check Price
    [Arguments]    ${element_with_checked_price}    ${products_count}    ${price_per_unit}
    ${number}=    Convert To Integer    ${price_per_unit}    
    ${total_price}=    Evaluate    ${number}*${products_count}
    ${total_price_string}=    Convert To String    ${total_price}
    Element Should Contain    ${element_with_checked_price}    ${total_price_string}
    
Remove From Cart And Close Browser
    Click Link    ${shop_cart_table}//tr[td/a[@title='${product_name}']]//a[@title='Odstranit položku']
    Page Should Contain    V košíku nejsou žádné položky.       
    Close Browser
    
Shopping Cart Basic Check
    Element Should Contain    ${shop_cart_title}    ${product_name}
    Element Should Contain    ${shop_cart_price}    ${product_price}
    Page Should Contain Element    ${shop_cart_qty_1}
    Check Price    ${shop_cart_total_price}    1    ${product_price}
    
Check Fields For Contact Details
    Page Should Contain Element    ${step_two_first_name}    
    Page Should Contain Element    ${step_two_last_name}    
    Page Should Contain Element    ${step_two_email}    
    Page Should Contain Element    ${step_two_street1}    
    Page Should Contain Element    ${step_two_city}    
    Page Should Contain Element    ${step_two_psc}    
    Page Should Contain Element    ${step_two_telephone}   
 
Invalid Continue Step Two Check 
    Click Button    ${step_two_continue}
    Wait Until Page Contains Element    ${step_two_active}  
    
Invalid Continue Step Two Check With Advice
    Invalid Continue Step Two Check
    Page Should Contain Element    ${step_two_validation_advice}   
    
Step Six Basic Check
    [Arguments]    ${name_product}    ${price_per_unit}    ${total_price}
    Element Should Contain    ${step_six_product}    ${name_product}    
    Element Should Contain    ${step_six_price}    ${price_per_unit}
    Element Should Contain    ${step_six_total_price}    ${total_price}
    Page Should Contain Element    ${step_six_submit}
    
Check URL
    [Arguments]    ${expected_url}
    ${url}=    Get Location
    Should Be Equal    ${url}    ${expected_url}