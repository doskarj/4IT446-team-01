*** Settings ***
Library    Selenium2Library
Resource    ../variables/variables.txt 
Resource    ../objects/objects_vydra.txt 
Resource    ../objects/objects_search.txt
Resource    ../objects/objects_success_msg.txt
Resource    ../objects/objects_shopping_cart.txt
*** Keywords ***
Open browser to Zoo eshop
    Open Browser    https://eshop.zoopraha.cz    browser=chrome
    Maximize Browser Window
    ${page_title}=    Get Title
    Should Contain    ${page_title}    ${eshop_page_title}
    
Navigate to souveniers/magnets
    Wait Until Element Is Visible    ${suveniers_url}
    Click Element    ${suveniers_url}
    Wait Until Element Is Visible    ${magnets_category}
    Click Element    ${magnets_category}
    ${page_title}=    Get Title
    Should Contain    ${page_title}    ${page_title_magnets}
    ${url}=    Get Location
    Should Be Equal    ${url}    ${magnets_page_url}
    
Check for product in list
    Wait Until Element Is Visible    ${vydra_url}
    Element Text Should Be    ${product_list_item}    ${product_name}
    Element Should Contain    xpath=//div[@class='item-area' and //h2/a[@title='${product_name}']]//span[@id='price-including-tax-1361']    ${product_price}
    Page Should Contain Element    xpath=//div[@class='item-area' and //h2/a[@title='${product_name}']]//img[@alt='${productImgAlt}']
    
Valid success message
    Wait Until Element Is Visible    ${success_msg_containter}
    Element Should Contain    ${success_msg_product}    ${product_name}
    Page Should Contain Element    ${success_msg_checkout}
    Page Should Contain Element    ${success_msg_continue}
    
Step from success message to shopping cart
    Click Element    ${success_msg_checkout}
    Page Should Contain Element    ${shop_cart_table}
    
Check header menu
    Page Should Contain Element    ${header_menu}
    
Add product to shopping cart from product list
     Mouse Over    xpath=//div[@class='item-area' and //h2/a[@title='${product_name}']]
    Click Link    xpath=//div[@class='item-area' and //h2/a[@title='${product_name}']]//a[@title='Přidat do košíku']
    
Check Price
    [Arguments]    ${element_with_checked_price}    ${products_count}
    ${number}=    Convert To Integer    ${product_price}    
    ${total_price}=    Evaluate    ${number}*${products_count}
    ${total_price_string}=    Convert To String    ${total_price}
    Element Should Contain    ${element_with_checked_price}    ${total_price_string}
    
Remove From Cart And Close Browser
    Click Link    ${shop_cart_table}//tr[td/a[@title='${product_name}']]//a[@title='Odstranit položku']
    Page Should Contain    V košíku nejsou žádné položky.       
    Close Browser